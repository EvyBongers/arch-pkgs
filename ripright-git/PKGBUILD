# Maintainer: Evy Bongers <evy@evybongers.nl>
pkgname=ripright-git
_pkgname="${pkgname%-git}"
pkgver=r19.94eb2a8
pkgrel=1
pkgdesc="A minimal automatic CD ripper and tagging utility"
arch=('x86_64')
url="https://github.com/marcoep/${_pkgname}"
license=('GPL2')
depends=('curl' 'flac' 'cdparanoia' 'libmagick6' 'libdiscid')
makedepends=()
optdepends=()
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha256sums=(SKIP)

pkgver() {
    cd "${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${_pkgname}"
    export PKG_CONFIG_PATH=$PKG_CONFIG_PATH:/usr/lib/imagemagick6/pkgconfig/
    env MagickWand_CFLAGS="`pkg-config --cflags MagickWand-6.Q16HDRI`" \
        MagickWand_LIBS="`pkg-config --libs MagickWand-6.Q16HDRI`" \
        ./configure --prefix=/usr
    make
}

package() {
    cd "${_pkgname}"
    make DESTDIR="$pkgdir/" install
}
